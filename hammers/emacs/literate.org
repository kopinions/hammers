#+TITLE:  Orgmode
#+AUTHOR: 孙建康（rising.lambda）
#+EMAIL:  rising.lambda@gmail.com

#+DESCRIPTION: 使用文学编程书写的，orgmode 的配置文件
#+PROPERTY:    header-args        :mkdirp yes
#+OPTIONS:     num:nil toc:nil todo:nil tasks:nil tags:nil
#+OPTIONS:     skip:nil author:nil email:nil creator:nil timestamp:nil
#+INFOJS_OPT:  view:nil toc:nil ltoc:t mouse:underline buttons:0 path:http://orgmode.org/org-info.js

*** 禁止 emacs 自带的 orgmode
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (assq-delete-all 'org package--builtins)
      (assq-delete-all 'org package--builtin-versions)
    #+END_SRC

*** 从系统软件中获取链接
    当系统为 macos 时，我们可以通过安装 [[https://github.com/xuchunyang/grab-mac-link.el][grab-mac-link]] 插件来获取 chrome/firefox/safia 中的链接，并且可以选择
    plain/markdown/orgmode 的形式来插入到当前编辑的文件中。

    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package grab-mac-link
	:if (eq m/os 'macos))
    #+END_SRC

*** org-mode 代码块异步执行配置
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package ob-async
	:hook
	(org-babel-after-execute . org-redisplay-inline-images))
    #+END_SRC

*** 截屏配置

    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (defun m/org-screenshot ()
	"Take a screenshot into a time stamped unique-named file in the
      same directory as the org-buffer and insert a link to this file."
	(interactive)
	(org-display-inline-images)
	(setq filename
	      (concat
	       (make-temp-name
		(concat (file-name-nondirectory (buffer-file-name))
			"_imgs/"
			(format-time-string "%Y%m%d_%H%M%S_")) ) ".png"))
	(unless (file-exists-p (file-name-directory filename))
	  (make-directory (file-name-directory filename)))
					      ; take screenshot
	(if (eq system-type 'darwin)
	    (call-process "screencapture" nil nil nil "-i" filename))
	(if (eq system-type 'gnu/linux)
	    (call-process "import" nil nil nil filename))
					      ; insert into file if correctly taken
	(if (file-exists-p filename)
	    (insert (concat "[[file:" filename "]]"))))
    #+END_SRC

*** org-mode 配置
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (setq org-agenda-dir (expand-file-name "agendas" m/home.d))
      (use-package org
	:diminish org-mode
	:custom
	(org-src-fontify-natively t)
	(org-descriptive-links nil)
	(org-special-ctrl-a/e t)
	(org-use-sub-superscripts '{})
	(org-export-with-sub-superscripts '{})
	(org-confirm-babel-evaluate nil)
	(org-startup-folded 'content)
	(org-edit-timestamp-down-means-later t)
	(org-hide-emphasis-markers t)
	(org-catch-invisible-edits 'show)
	(org-export-coding-system 'utf-8)
	(org-pretty-entities t)
	(org-fast-tag-selection-single-key 'expert)
	(org-image-actual-width nil)
	(org-adapt-indentation t)
	:custom-face
	(org-link ((t (:foreground "#ebe087" :underline t))))
	(org-list-dt ((t (:foreground "#bd93f9"))))
	(org-document-title ((t (:foreground "#f1fa8c" :weight bold))))
	(org-footnote ((t (:foreground "#76e0f3"))))
	;; 当通过 org-previous/next-item 在 plain list 中导航的时候，循环导航 
	(org-list-use-circular-motion t)

	;; do not scale outline header
	;; (org-level-1 ((t (:inherit outline-1 :height 1.0))))
	;; (org-level-2 ((t (:inherit outline-2 :height 1.0))))
	;; (org-level-3 ((t (:inherit outline-3 :height 1.0))))
	;; (org-level-4 ((t (:inherit outline-4 :height 1.0))))
	;; (org-level-5 ((t (:inherit outline-5 :height 1.0))))
	:bind 
	(("C-c b" . org-switchb)
	 ("C-x C-l" . org-store-link)
	 :map org-mode-map
	 ("C-c n" . org-narrow-to-subtree)
	 ("C-c b" . org-narrow-to-block)
	 ("C-c {" . sp-wrap-curly)
	 ("C-c (" . sp-wrap-round)
	 ("C-c [" . sp-wrap-square)
	 ("C-c w" . widen)
	 ("S-<up>" . org-previous-item)
	 ("S-<down>" . org-next-item)
	 )
	:hook
	(org-mode . (lambda ()
		      (dolist (key '("C-'" "C-," "C-."))
			(unbind-key key org-mode-map))))
	:config
	(advice-add 'org-refile :after (lambda (&rest _) (org-save-all-org-buffers)))
	;; load babel languages
	(org-babel-do-load-languages
	 'org-babel-load-languages
	 '((R . t)
	   (dot . t)
	   (ditaa . t)
	   (emacs-lisp . t)
	   (gnuplot . t)
	   (haskell . nil)
	   (latex . t)
	   (ocaml . nil)
	   (octave . t)
	   (plantuml . t)
	   (python . t)
	   (ruby . t)
	   (screen . nil)
	   (shell . t)
	   (sql . t)
	   (sqlite . t))))

    #+END_SRC

*** org-mode archive 配置
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (defadvice org-archive-subtree (around fix-hierarchy activate)
	(let* ((fix-archive-p (and (not current-prefix-arg)
				   (not (use-region-p))))
	       (afile  (car (org-archive--compute-location
			     (or (org-entry-get nil "ARCHIVE" 'inherit) org-archive-location))))
	       (buffer (or (find-buffer-visiting afile) (find-file-noselect afile))))
	  ad-do-it

	  (when fix-archive-p
	    (with-current-buffer buffer
	      (goto-char (point-max))
	      (while (org-up-heading-safe))
	      (let* ((olpath (org-entry-get (point) "ARCHIVE_OLPATH"))
		     (path (and olpath (split-string olpath "/")))
		     (level 1)
		     tree-text)
		(when olpath
		  (org-mark-subtree)
		  (setq tree-text (buffer-substring (region-beginning) (region-end)))
		  (let (this-command) (org-cut-subtree))
		  (goto-char (point-min))
		  (save-restriction
		    (widen)
		    (-each path
		      (lambda (heading)
			(if (re-search-forward
			     (rx-to-string
			      `(: bol (repeat ,level "*") (1+ " ") ,heading)) nil t)
			    (org-narrow-to-subtree)
			  (goto-char (point-max))
			  (unless (looking-at "^")
			    (insert "\n"))
			  (insert (make-string level ?*)
				  " "
				  heading
				  "\n"))
			(cl-incf level)))
		    (widen)
		    (org-end-of-subtree t t)
		    (org-paste-subtree level tree-text))))))))
    #+END_SRC

*** org-mode 日程配置
    
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package org
	:preface
	(defun m/refile-targets ()
	  "Refile targets with inbox.org excluded"
	  (let ((excluded '("mobile.org" "inbox.org" "notes.org" "meeting.org" "retrospect.org" "archive.org")))
	    (seq-filter (lambda (f) (if (member (file-name-nondirectory f) excluded) nil t)) org-agenda-files)))
	:custom
	(org-directory "agendas")
	;; Save state changes in the LOGBOOK drawer
	(org-log-into-drawer t)
	(org-log-done t)
	(org-agenda-include-diary t)
	(org-agenda-time-grid
	 '((daily today require-timed)
	   (800 1000 1200 1400 1600 1800 2000)
	   " ........ "
	   "---------------------------"))
	(org-clock-out-remove-zero-time-clocks t)
	(org-columns-default-format "%50ITEM(Task) %5TODO(Todo) %10Effort(Effort){:} %10CLOCKSUM(Clock) %2PRIORITY %TAGS")
	(org-agenda-columns-add-appointments-to-effort-sum t)
	(org-agenda-span 'day)
	(org-agenda-skip-scheduled-if-done t)
	(org-agenda-skip-deadline-if-done t)
	(org-deadline-warning-days 3)
	(org-agenda-log-mode-items (quote (closed clock)))
	(org-agenda-clockreport-parameter-plist
	 '(:maxlevel 5 :block t :tstart t :tend t :emphasize t :link nil :narrow 80 :indent t :compact t :fileskip0 t :formula % :level 4 :tcolumns nil :formatter nil))
	(org-global-properties (quote ((
					"Effort_ALL" . "00:05 00:10 00:15 00:30 01:00 01:30 02:00 02:30 03:00"))))
	(org-agenda-files `(
			    ,(expand-file-name "inbox.org" org-agenda-dir)
			    ,(expand-file-name "todos.org" org-agenda-dir)
			    ,(expand-file-name "mobile.org" org-agenda-dir)
			    ,(expand-file-name "routine.org" org-agenda-dir)
			    ,(expand-file-name "meeting.org" org-agenda-dir)
			    ,(expand-file-name "retrospect.org" org-agenda-dir)
			    ))
	(org-archive-location (concat (expand-file-name "archive.org" org-agenda-dir) "::"))
	(org-archive-mark-done t)
	(org-support-shift-select t)

	;; Targets start with the file name - allows creating level 1 tasks
	;;(setq org-refile-use-outline-path (quote file))
	(org-refile-use-outline-path t)
	(org-outline-path-complete-in-steps nil)

	;; Allow refile to create parent tasks with confirmation
	(org-refile-use-cache t)

	(org-refile-allow-creating-parent-nodes 'confirm)
	(org-refile-targets '((m/refile-targets :maxlevel . 5)))
	      ;;; Agenda views
	(org-agenda-custom-commands
	 `(("N" "Notes" tags "NOTE"
	    ((org-agenda-overriding-header "Notes")
	     (org-tags-match-list-sublevels t)))
	   ("a" "Agenda"
	    ((agenda "" ((org-agenda-span 1)                      ; daily agenda
			 (org-deadline-warning-days 3)            ; 7 day advanced warning for deadlines
			 (org-agenda-scheduled-leaders '("" "Due %2dx: "))
			 (org-agenda-overriding-header "Today's Schedule:")
			 (org-agenda-start-day nil)
			 (org-agenda-archives-mode t)			 
			 (org-agenda-start-on-weekday nil)
			 (org-agenda-compact-blocks t)
			 (org-agenda-use-time-grid nil)))
	     (agenda "" ((org-agenda-span 7)                      ;; overview of appointments
			 (org-agenda-start-on-weekday nil)         ;; calendar begins today
			 (org-agenda-scheduled-leaders '("" "Due %2dx: "))
			 (org-agenda-deadline-leaders '("" "Due in %2dx: "))
			 (org-agenda-start-day "+1d")
			 (org-agenda-show-future-repeats nil)
			 (org-agenda-archives-mode t)			 
			 (org-agenda-entry-types '(:timestamp :sexp :scheduled :deadline))))))
	   ("g" "GTD"
	    ((agenda "" ((org-agenda-span 1)                      ; daily agenda
			 (org-deadline-warning-days 3)            ; 7 day advanced warning for deadlines
			 (org-agenda-scheduled-leaders '("" "Due %2dx: "))
			 (org-agenda-overriding-header "Today's Schedule:")
			 (org-agenda-start-day nil)
			 (org-agenda-archives-mode t)			 
			 (org-agenda-start-on-weekday nil)
			 (org-agenda-compact-blocks t)
			 (org-agenda-use-time-grid t)))
	     (tags "INBOX"
		   ((org-agenda-overriding-header "Inbox")
		    (org-agenda-skip-function
		     '(lambda ()
			(or (org-agenda-skip-subtree-if 'todo '("DONE"))
			    (org-agenda-skip-entry-if 'nottodo '("TODO" "NEXT")))))
		    (org-tags-match-list-sublevels t)))
	     (tags-todo "-INBOX"
			((org-agenda-overriding-header "Next Actions")
			 (org-agenda-tags-todo-honor-ignore-options t)
			 (org-agenda-todo-ignore-scheduled 'all)
			 (org-agenda-todo-ignore-deadlines 'all)
			 (org-agenda-skip-function
			  '(lambda ()
			     (or (org-agenda-skip-subtree-if 'todo '("HOLD" "WAITING"))
				 (org-agenda-skip-entry-if 'nottodo '("NEXT")))))
			 (org-tags-match-list-sublevels t)
			 (org-agenda-sorting-strategy
			  '(todo-state-down effort-up category-keep))))
	     (stuck ""
		    ((org-agenda-overriding-header "Stuck Projects")
		     (org-agenda-tags-todo-honor-ignore-options t)
		     (org-tags-match-list-sublevels t)
		     (org-agenda-files `(
					 ,(expand-file-name "todos.org" org-agenda-dir)
					 ,(expand-file-name "routine.org" org-agenda-dir)
					 ))
		     (org-agenda-skip-function
		      '(lambda ()
			 (org-agenda-skip-entry-if 'nottodo '("PROJECT"))))
		     (org-agenda-todo-ignore-deadlines 'all)
		     (org-agenda-todo-ignore-scheduled 'all)))
	     (tags-todo "-INBOX/PROJECT"
			((org-agenda-overriding-header "Projects")
			 (org-tags-match-list-sublevels t)
			 (org-agenda-sorting-strategy
			  '(category-keep))))
	     (tags-todo "-INBOX/-NEXT"
			((org-agenda-overriding-header "Orphaned Tasks")
			 (org-agenda-tags-todo-honor-ignore-options t)
			 (org-agenda-todo-ignore-scheduled 'all)
			 (org-agenda-todo-ignore-deadlines 'all)
			 (org-agenda-skip-function
			  '(lambda ()
			     (or (org-agenda-skip-subtree-if 'todo '("PROJECT" "HOLD" "WAITING" "DELEGATED"))
				 (org-agenda-skip-subtree-if 'nottododo '("TODO")))))
			 (org-tags-match-list-sublevels t)
			 (org-agenda-sorting-strategy
			  '(category-keep))))
	     (tags-todo "/WAITING"
			((org-agenda-overriding-header "Waiting")
			 (org-agenda-tags-todo-honor-ignore-options t)
			 (org-agenda-todo-ignore-scheduled 'all)
			 (org-agenda-todo-ignore-deadlines 'all)
			 (org-agenda-sorting-strategy
			  '(category-keep))))
	     (tags-todo "/DELEGATED"
			((org-agenda-overriding-header "Delegated")
			 (org-agenda-tags-todo-honor-ignore-options t)
			 (org-agenda-todo-ignore-scheduled 'all)
			 (org-agenda-todo-ignore-deadlines 'all)
			 (ORG-agenda-sorting-strategy
			  '(category-keep))))
	     (tags-todo "-INBOX"
			((org-agenda-overriding-header "On Hold")
			 (org-agenda-skip-function
			  '(lambda ()
			     (or (org-agenda-skip-subtree-if 'todo '("WAITING"))
				 (org-agenda-skip-entry-if 'nottodo '("HOLD")))))
			 (org-tags-match-list-sublevels nil)
			 (org-agenda-sorting-strategy
			  '(category-keep))))

	     ;; (tags-todo "-NEXT"
	     ;;            ((org-agenda-overriding-header "All other TODOs")
	     ;;             (org-match-list-sublevels t)))
	     ))))
	:bind
	(("C-c c" . org-capture)
	 ("C-c a" . org-agenda)
	 :map org-mode-map
	 ("C-c i" . org-clock-in)
	 ("C-c o" . org-clock-out)
	 ("C-c e" . org-set-effort))
	:hook
	(org-agenda-after-show . org-show-entry)
	(org-agenda-mode . hl-line-mode)
	:custom-face
	(org-special-keyword ((t (:foreground "#6272a4"))))
	(org-todo ((t (:background "#272934" :foreground "#51fa7b" :weight bold))))
	(org-done ((t (:background "#373844" :foreground "#216933" :strike-through nil :weight bold))))
	:config
	(advice-add 'org-deadline       :after (lambda (&rest _rest)  (org-save-all-org-buffers)))
	(advice-add 'org-schedule       :after (lambda (&rest _rest)  (org-save-all-org-buffers)))
	(advice-add 'org-agenda-schedule       :after (lambda (&rest _rest)  (org-save-all-org-buffers)))
	(advice-add 'org-agenda-capture       :after (lambda (&rest _rest)  (org-save-all-org-buffers)))
	(advice-add 'org-store-log-note :after (lambda (&rest _rest)  (org-save-all-org-buffers)))
	(advice-add 'org-todo           :after (lambda (&rest _rest)  (org-save-all-org-buffers)))
	(setq org-todo-keywords
	      (quote (
		      (sequence "TODO(t)" "NEXT(n)" "|" "DONE(d!/!)")
		      (sequence "PROJECT(p)" "|" "DONE(d!/!)" "CANCELLED(c@/!)")
		      (sequence "WAITING(w@/!)" "DELEGATED(e!)" "HOLD(h)" "|" "CANCELLED(c@/!)")
		      ))
	      org-todo-repeat-to-state "NEXT")
	(setq org-todo-keyword-faces
	      '(("WAIT" . (:foreground "#6272a4":weight bold))
		("NEXT"   . (:foreground "#f1fa8c" :weight bold))
		("CARRY/O" . (:foreground "#6272a4" :background "#373844" :weight bold)))))
    #+END_SRC
*** org-mode capture 配置
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package org
	:custom
	(org-capture-templates
	 `(
	   ;; tasks
	   ("t" "general task." entry
	    (file ,(expand-file-name "inbox.org" org-agenda-dir))
	    ,(mapconcat 'identity
			'("* NEXT %?"
			  "  :PROPERTIES:"
			  "  :ID: %(org-id-uuid)"
			  "  :CAPTURED_AT: %U"
			  "  :LOGGING: DONE CANCELLED(@/!) nologrepeat"
			  "  :END:")
			"\n")
	    :clock-resume 1)
	   ("n" "notes." entry
	    (file ,(expand-file-name "notes.org" org-agenda-dir))
	    "* %? :NOTE:\n" :clock-resume t)
	   ("c" "code snippet" plain (file ,(expand-file-name "snippets.org" org-agenda-dir))
	    ,(concat "%[" (expand-file-name "snippet.tmpl" org-agenda-dir) "]")
	    :tree-type week)
	   ("v" "vocabulary" table-line (file+headline ,(expand-file-name "vocabulary.org" org-agenda-dir) "vocabularies")
	    "|%?||||")
	   ("a" "acronym" table-line (file+headline ,(expand-file-name "vocabulary.org" org-agenda-dir) "acronyms")
	    "|%?|||")
	   ("r" "retrospective note" plain (file+olp+datetree ,(expand-file-name "retrospect.org" org-agenda-dir))
	    ,(concat "%[" (expand-file-name "retrospect.tmpl" org-agenda-dir) "]")
	    :tree-type week)
	   ("m" "meeting note" plain (file+olp+datetree ,(expand-file-name "meeting.org" org-agenda-dir))
	    ,(concat "%[" (expand-file-name "meeting.tmpl" org-agenda-dir) "]")
	    :tree-type week :empty-lines 1 :kill-buffer 1)
	   ("i" "interrupt task" entry
	    (file ,(expand-file-name "inbox.org" org-agenda-dir))
	    "* NEXT %^{what has interrupted?}" :clock-in 1 :clock-keep 1)
	   ("e" "Collect hacking Emacs ideas!" item
	    (file+headline ,(expand-file-name "inbox.org" org-agenda-dir) "Hacking Emacs")
	    "- [ ] %?"
	    :prepend t)
	   ("p" "Add an event to the private calendar." entry
	    (file+olp schedule-file "Calendar" "2019" "Private")
	    "** %?\n   SCHEDULED: <%(org-read-date)>\n"
	    :prepend t)
	   ("w" "Add an event to the work calendar." entry
	    (file+olp schedule-file "Calendar" "2019" "Work")
	    "** %?\n   SCHEDULED: <%(org-read-date)>\n")
	   ("l" "Store the link of the current position in the clocking task." item
	    (clock)
	    "- %A\n"
	    :immediate t :prepend t))))
    #+END_SRC
*** org-mode capture command line support
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (defmacro letf! (bindings &rest body)
	"Temporarily rebind function, macros, and advice in BODY.
      Intended as syntax sugar for `cl-letf', `cl-labels', `cl-macrolet', and
      temporary advice.
      BINDINGS is either:
	A list of, or a single, `defun', `defun*', `defmacro', or `defadvice' forms.
	A list of (PLACE VALUE) bindings as `cl-letf*' would accept.
      TYPE is one of:
	`defun' (uses `cl-letf')
	`defun*' (uses `cl-labels'; allows recursive references),
	`defmacro' (uses `cl-macrolet')
	`defadvice' (uses `defadvice!' before BODY, then `undefadvice!' after)
      NAME, ARGLIST, and BODY are the same as `defun', `defun*', `defmacro', and
      `defadvice!', respectively.
      \(fn ((TYPE NAME ARGLIST &rest BODY) ...) BODY...)"
	(declare (indent defun))
	(setq body (macroexp-progn body))
	(when (memq (car bindings) '(defun defun* defmacro defadvice))
	  (setq bindings (list bindings)))
	(dolist (binding (reverse bindings) body)
	  (let ((type (car binding))
		(rest (cdr binding)))
	    (setq
	     body (pcase type
		    (`defmacro `(cl-macrolet ((,@rest)) ,body))
		    (`defadvice `(progn (defadvice! ,@rest)
					(unwind-protect ,body (undefadvice! ,@rest))))
		    ((or `defun `defun*)
		     `(cl-letf ((,(car rest) (symbol-function #',(car rest))))
			(ignore ,(car rest))
			,(if (eq type 'defun*)
			     `(cl-labels ((,@rest)) ,body)
			   `(cl-letf (((symbol-function #',(car rest))
				       (fn! ,(cadr rest) ,@(cddr rest))))
			      ,body))))
		    (_
		     (when (eq (car-safe type) 'function)
		       (setq type (list 'symbol-function type)))
		     (list 'cl-letf (list (cons type rest)) body)))))))

      (defvar org-capture-initial)

      ;;;###autoload
      (defvar fallback-buffer-name "*scratch*"
	"The name of the buffer to fall back to if no other buffers exist (will create
      it if it doesn't exist).")

      ;;;###autoload
      (defun fallback-buffer ()
	"Returns the fallback buffer, creating it if necessary. By default this is the
      scratch buffer. See `doom-fallback-buffer-name' to change this."
	(let (buffer-list-update-hook)
	  (get-buffer-create fallback-buffer-name)))

      (defvar +org-capture-fn #'org-capture
	"Command to use to initiate org-capture.")

      ;;;###autoload
      (defvar +org-capture-frame-parameters
	`((name . "org-capture")
	  (width . 70)
	  (height . 25)
	  (transient . t)
	  ,(when (and (eq m/os 'linux) (not (getenv "DISPLAY")))
	     `(display . ":0"))
	  ,(if (eq m/os 'macos) '(menu-bar-lines . 1)))
	"TODO")

      ;;;###autoload
      (defun +org-capture-frame-p (&rest _)
	"Return t if the current frame is an org-capture frame opened by
      `+org-capture/open-frame'."
	(and (equal (alist-get 'name +org-capture-frame-parameters)
		    (frame-parameter nil 'name))
	     (frame-parameter nil 'transient)))

      ;;;###autoload
      (defun m/org-capture-open-frame (&optional initial-input key)
	"Opens the org-capture window in a floating frame that cleans itself up once
      you're done. This can be called from an external shell script."
	(interactive)
	(when (and initial-input (string-empty-p initial-input))
	  (setq initial-input nil))
	(when (and key (string-empty-p key))
	  (setq key nil))
	(let* ((frame-title-format "")
	       (frame (if (+org-capture-frame-p)
			  (selected-frame)
			(make-frame +org-capture-frame-parameters))))
	  (select-frame-set-input-focus frame)  ; fix MacOS not focusing new frames
	  (with-selected-frame frame
	    (require 'org-capture)
	    (condition-case ex
		(letf! ((#'pop-to-buffer #'switch-to-buffer)
			(capture-frame frame))
		  ;; (switch-to-buffer (fallback-buffer))
		  (delete-frame frame)
		  (let ((org-capture-initial initial-input)
			org-capture-entry)
		    (when (and key (not (string-empty-p key)))
		      (setq org-capture-entry (org-capture-select-template key)))
		    (funcall +org-capture-fn)))
	      ('error
	       (message "org-capture: %s" (error-message-string ex))
	       (delete-frame frame))))))

    #+END_SRC
*** org-mode capture command line tool
    #+BEGIN_SRC shell :eval never :exports code :tangle (m/resolve "${m/conf.d}/bin/capture") :tangle-mode (identity #o755) :comments link :noweb yes
      #!/usr/bin/env sh

      # Open an org-capture popup frame from the shell. This opens a temporary emacsp
      # daemon if emacs isn't already running.
      #
      # Usage: org-capture [-k KEY] [MESSAGE]
      # Examples:
      #   org-capture -k n "To the mind that is still, the whole universe surrenders."

      set -e

      cleanup() {
	  emacsclient --eval '(let (kill-emacs-hook) (kill-emacs))'
      }

      # If emacs isn't running, we start a temporary daemon, solely for this window.
      if ! emacsclient --suppress-output --eval nil; then
	  emacs --daemon
	  trap cleanup EXIT INT TERM
	  daemon=1
      fi

      # org-capture key mapped to argument flags
      # keys=$(emacsclient -e "(+org-capture-available-keys)" | cut -d '"' -f2)
      while getopts "hk:" opt; do
	  key="\"$OPTARG\""
	  break
      done
      shift $((OPTIND-1))

      # use remaining args, else read from stdin if passed a single dash
      str="$*"
      case "$str" in
	  -) str=$(cat) ;;
      esac

      # Fix incompatible terminals that cause odd 'not a valid terminal' errors
      [ "$TERM" = "alacritty" ] && export TERM=xterm-256color

      if [ $daemon ]; then
	  emacsclient -a "" \
		      -c -F '((name . "org-capture") (width . 70) (height . 25) (transient . t))' \
		      -e "(m/org-capture-open-frame \"$str\" ${key:-nil})"
      else
	  # Non-daemon servers flicker a lot if frames are created from terminal, so we
	  # do it internally instead.
	  emacsclient -a "" \
		      -e "(m/org-capture-open-frame \"$str\" ${key:-nil})"
      fi
    #+END_SRC

*** org-mode clock 配置
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package org
	:custom
	(org-clock-in-resume t)
	;; Save clock data and notes in the LOGBOOK drawer
	(org-clock-into-drawer t)
	;; Removes clocked tasks with 0:00 duration
	(org-clock-out-remove-zero-time-clocks t)
	;; Show clock sums as hours and minutes, not "n days" etc.
	(org-time-clocksum-format '(:hours "%d" :require-hours t :minutes ":%02d" :require-minutes t))
	;; (org-agenda-current-time-string "------------NOW------------")
	(org-clock-persist t)
	:preface
	(defun m/org-clock-out-and-save-when-exit ()
	  "Save buffers and stop clocking when kill emacs."
	  (ignore-errors (org-clock-out) t)
	  (save-some-buffers t))
	(defun m/pomodoro/reset ()
	  "timer update"
	  (if (and (boundp 'm/pomodoro/task/current) (timerp m/pomodoro/task/current))
	      (cancel-timer m/pomodoro/task/current)
	    (setq m/pomodoro/task/current nil))

	  (if (and (boundp 'm/pomodoro/task/next) (timerp m/pomodoro/task/next))
	      (cancel-timer m/pomodoro/task/next)
	    (setq m/pomodoro/task/next nil))

	  (if (and (boundp 'm/pomodoro/status/updater) (timerp m/pomodoro/status/updater))
	      (cancel-timer m/pomodoro/status/updater)
	    (setq m/pomodoro/status/updater nil)))
	(defun m/clockin ()
	  "clockin hook"
	  (m/pomodoro/reset)
	  (setq m/pomodoro/status/updater (run-at-time 0 60 '(lambda ()
							       (setq org-mode-line-string (m/task-clocked-time))
							       (force-mode-line-update))))
	  (setq m/pomodoro/task/current (run-at-time (* m/pomodoro/focus 60) nil (lambda() (org-clock-out)))))
	(defun m/clockout ()
	  "clock out hook"
	  (m/pomodoro/reset)
	  (setq m/pomodoro/task/next (run-at-time (* m/pomodoro/break 60) nil (lambda()
										(ignore-errors
										  (request "http://127.0.0.1:13140"
										    :type "POST"
										    :data (json-encode `(("type" . "FOCUS")
													 ("title" . "Ready to work")
													 ("duration" . 5)))
										    :headers '(("Content-Type" . "application/json"))))))))
	(defun m/task-clocked-time ()
	  "Return a string with the clocked time and effort, if any"
	  (interactive)
	  (let* ((clocked-time (org-clock-get-clocked-time))
		 (h (truncate clocked-time 60))
		 (m (mod clocked-time 60))
		 (work-done-str (format "%d:%02d" h m)))
	    (if org-clock-effort
		(let* ((effort-in-minutes
			(org-duration-to-minutes org-clock-effort))
		       (effort-h (truncate effort-in-minutes 60))
		       (effort-m (truncate (mod effort-in-minutes 60)))
		       (effort-str (format "%d:%02d" effort-h effort-m)))
		  (format "%s/%s" work-done-str effort-str))
	      (format "%s" work-done-str))))
	:config
	(setq m/pomodoro/focus 45)
	(setq m/pomodoro/break 5)
	(setq m/pomodoro/task/current nil)
	(setq m/pomodoro/task/next nil)
	(setq m/pomodoro/status/updater nil)
	:hook
	(kill-emacs . m/org-clock-out-and-save-when-exit)
	(org-clock-in .
		      (lambda ()
			(m/clockin)
			(let* ((title (if (string-match org-link-bracket-re org-clock-current-task)  (replace-regexp-in-string org-link-bracket-re "\\2" org-clock-current-task) (or org-clock-current-task "interrupt task"))))
			  (ignore-errors (request "http://127.0.0.1:13140"
					   :type "POST"
					   :data (json-encode `(("type" . "FOCUSED")
								("title" . ,title)
								("duration" . 45)))
					   :headers '(("Content-Type" . "application/json")))))))
	(org-clock-out . (lambda ()
			   (m/clockout)
			   (ignore-errors
			     (request "http://127.0.0.1:13140"
			       :type "POST"
			       :data (json-encode `(("type" . "UNFOCUSED")
						    ("title" . "Have a rest")
						    ("duration" . 5)))
			       :headers '(("Content-Type" . "application/json"))))))
	(org-clock-in-last . (lambda ()
			       (m/clockin)
			       (let* ((title (if (string-match org-link-bracket-re org-clock-current-task) (replace-regexp-in-string org-link-bracket-re "\\2" org-clock-current-task) (or org-clock-current-task "interrupt task"))))
				 (ignore-errors
				   (request "http://127.0.0.1:13140"
				     :type "POST"
				     :data (json-encode `(("type" . "FOCUSED")
							  ("title" . ,title)
							  ("duration" . 45)))
				     :headers '(("Content-Type" . "application/json"))))))))
    #+END_SRC
*** org-mode export 配置
    允许在 org 文件中通过 bind 修改环境中绑定的值。
    #+BEGIN_SRC elisp :exports code :eval never  :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package org
	:custom
	(org-export-allow-bind-keywords t))
    #+END_SRC

*** org-mode ditaa/plantuml 画图

    #+BEGIN_SRC elisp :exports code :eval never :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package org
	:custom
	(org-ditaa-jar-path (expand-file-name "ditaa.jar" m/conf.d))
	(org-plantuml-jar-path (expand-file-name "plantuml.jar" m/conf.d))
	:config
	(unless (and (boundp 'org-ditaa-jar-path)
		     (file-exists-p org-ditaa-jar-path)
		     (not (file-directory-p org-ditaa-jar-path)))
	  (let ((jar-name "ditaa.jar")
		(url "https://github.com/stathissideris/ditaa/releases/download/v0.11.0/ditaa-0.11.0-standalone.jar"))
	    (setq org-ditaa-jar-path (expand-file-name jar-name m/conf.d))
	    (unless (file-exists-p org-ditaa-jar-path)
	      (url-copy-file url org-ditaa-jar-path))))

	(unless (and (boundp 'org-plantuml-jar-path)
		     (file-exists-p org-plantuml-jar-path)
		     (not (file-directory-p org-plantuml-jar-path)))
	  (let ((jar-name "plantuml.jar")
		(url "https://downloads.sourceforge.net/project/plantuml/1.2020.2/plantuml.1.2020.2.jar"))
	    (setq org-plantuml-jar-path (expand-file-name jar-name m/conf.d))
	    (unless (file-exists-p org-plantuml-jar-path)
	      (url-copy-file url org-plantuml-jar-path)))))

      (use-package plantuml-mode
	:after org
	:config
	(add-to-list
	 'org-src-lang-modes '("plantuml" . plantuml))
	(setq plantuml-jar-path org-plantuml-jar-path)
	(setq plantuml-default-exec-mode 'jar))
    #+END_SRC

*** org-mode graphiz 画图配置
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package graphviz-dot-mode
	:after org
	:config
	(setq graphviz-dot-indent-width 4))
      (use-package company-graphviz-dot
	:after company
	:ensure nil)
    #+END_SRC
*** org mode 可以拖拽下载
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      ;; Download Drag&Drop images
      (use-package org-download
	:after org)
    #+END_SRC

*** org mode 图标设置
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      ;; Pretty bullets
      (use-package org-bullets
	:after org
	:hook (org-mode . org-bullets-mode)
	:config
	(setq org-bullets-face-name (quote org-bullet-face))
	(org-bullets-mode 1)
	(setq org-bullets-bullet-list '("✙" "♱" "♰" "☥" "✞" "✟" "✝" "†" "✠" "✚" "✜" "✛" "✢" "✣" "✤" "✥")))
    #+END_SRC

*** org-mode 博客
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package ox-gfm)
      (use-package ox-hugo
	:after (ox org)
	:custom
	(org-blackfriday--org-element-string '((src-block . "Code")
					       (table . "Table")
					       (figure . "Figure"))))
    #+END_SRC

*** org-mode 中文对齐配置
    #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
      (use-package valign
	:custom
	(valign-fancy-bar 't)
	:hook
	(org-agenda-finalize . valign-region)
	(org-agenda-mode . valign-mode))
    #+END_SRC
*** org-mode retrospect temlate
    #+BEGIN_SRC org :tangle (m/resolve "${m/home.d}/agendas/retrospect.tmpl") :noweb yes :mkdirp yes
      %(make-string 2 ? ):PROPERTIES:
      %(make-string 2 ? ):ID:       %(org-id-uuid)
      %(make-string 2 ? ):CAPTURED_AT: %U
      %(make-string 2 ? ):END:
      %(make-string 2 ? )|----------------------------+----|
      %(make-string 2 ? )| 我的长远目标是什么？       | %? |
      %(make-string 2 ? )|----------------------------+----|
      %(make-string 2 ? )| 昨天学到什么？             |    |
      %(make-string 2 ? )|----------------------------+----|
      %(make-string 2 ? )| 昨天得到什么教训？         |    |
      %(make-string 2 ? )|----------------------------+----|
      %(make-string 2 ? )| 费曼学习法实践案例？       |    |
      %(make-string 2 ? )|----------------------------+----|
      %(make-string 2 ? )| 昨天做了哪些换位思考？     |    |
      %(make-string 2 ? )|----------------------------+----|
      %(make-string 2 ? )| 昨天做了哪些倒推思维训练？ |    |
      %(make-string 2 ? )|----------------------------+----|
      %(make-string 2 ? )| 昨天发生的事情             |    |
      %(make-string 2 ? )| 我能提出什么问题           |    |
      %(make-string 2 ? )|----------------------------+----|
      %(make-string 2 ? )| 接下来有什么计划？         |    |
      %(make-string 2 ? )|                            |    |
      %(make-string 2 ? )|                            |    |
      %(make-string 2 ? )|                            |    |
      %(make-string 2 ? )|----------------------------+----|
    #+END_SRC

*** org-mode code template
    #+BEGIN_SRC org :tangle (m/resolve "${m/home.d}/agendas/snippet.tmpl") :noweb yes :mkdirp yes
      ,**** %^{purpose of the snippet} 
	   ,#+BEGIN_SRC %^{language|shell|elisp|dot|ditaa|latex|ocaml|plantuml|org|c|c++} :eval never :exports code
	       %?
	   ,#+END_SRC
    #+END_SRC
*** org-mode meeting template
    #+BEGIN_SRC org :tangle (m/resolve "${m/home.d}/agendas/meeting.tmpl") :noweb yes :mkdirp yes
      ,**** %^{会议主题}
	   |--------------+----|
	   | 哪些 Action? | %? |
	   |--------------+----|
	   | 有哪些洞见？ |    |
	   |--------------+----|
	   | 有那些问题？ |    |
	   |--------------+----|
    #+END_SRC

*** org-mode gtd files
    #+BEGIN_SRC org :tangle no :noweb yes :mkdirp yes :comments link
      ,#+CATEGORY: Inbox
      ,#+FILETAGS: INBOX
    #+END_SRC
    
** provide
   #+BEGIN_SRC elisp :eval never :exports code :tangle (m/resolve "${m/conf.d}/lisp/init-literate.el") :comments link
     (provide 'init-literate)
   #+END_SRC
